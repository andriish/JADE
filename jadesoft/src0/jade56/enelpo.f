C   25/06/78 708272232  MEMBER NAME  ENELPO   (S)           FORTRAN
      FUNCTION ENELPO(E)
C
C THIS FUNCTION GIVES THE AVERAGE IONIZATION LOSS, I.E. ENERGY LOSS
C FOR POSITRONS.
C THIS IS FORMULA (14) IN UEHLING, ANN.REV.NUCL.PHYS. 4 (1954)315
C
C  NOTE THAT THE INPUT ENERGY COMES IN UNITS OF ME, I.E. == GAMMA,
C  THEREFORE     BETA**2 = 1 - 1/E**2
C  LN2 = .69315
C
C  COMMON/ILPAR/ IS SET IN PARST2, CALLED BY INTSHW
C
      COMMON/ILPAR/AI,BI,ALCUTM
      DATA RME/0.511/
C
      BE2=1.-1./E**2
      IF(BE2.LE.0.)GOTO10
      EKIN = (E-1.)*RME
C
      CON=ALOG(E)+ALOG(BE2)/2.
C
      BIPRIM = BI + .69315
C
      ENELPO=AI*(BIPRIM + ALOG(BE2) - ALOG(1.-BE2) + ALOG(EKIN)
     @ - (BE2/12.) * ( 23. + 14./(E+1.) + 10./(E+1.)**2 + 4./(E+1.)**3 )
     @         -DENDEL(0.434294*CON))/BE2
C FUNCTION DENDEL CALCULATES DENSITY EFFECT CORRECTION.
      RETURN
 10   ENELPO=0.
      RETURN
      END
