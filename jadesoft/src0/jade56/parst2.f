C   25/06/78 705272213  MEMBER NAME  PARST2   (S)           FORTRAN
      SUBROUTINE PARST2(Z,A,T0,PCUTM,PLE)
C
C     A.SATO
C
C     SET PARAMETERS FOR IONIZATION LOSS
C     THIS ROUTINE VALID ONLY FOR DENSE MATERIAL WITH
C      (Z.GE.6) AND (I.GE.100.).
C     HERE I IS IONIZATION POTENTIAL IN EV.
C     IN THE PROGRAM IT IS WRITTEN AS RI.
C---- REFERENCE   R.M.STEINHEIMER ET AL,PHYS.REV.B3,3681(1971)
C
C  COMMENTS BY J.OLSSON   27.5.1987
C
C    AI AND BI COME FROM FORMULAS (25) AND (26)
C    I IS GIVEN BY FORMULA (13)
C    NOTE THAT THE NORMALIZATION CONSTANT AI, WHICH IN THE REF. HAS
C    THE NUMBER 0.1536 IN IT, HERE IS GIVEN IN UNITS OF EL.MASS, I.E.
C    0.1536/ME = 0.3006
C    NOTE THAT THE STERNHEIMER FORMULA GIVES ENERGY LOSS IN UNIT OF
C    MEV CM**2 / G. THIS IS TURNED INTO UNIT OF MEV BY MULTIPLICATION
C    WITH T0, WHICH HAS UNIT OF G/CM**2
C    26.96 = LN(ME) + LN(10**6)
C
C
      COMMON /ILPAR/AI,BI,CUTM
      COMMON /DFPAR/CI,SAI,RMI,X0,X1
C
      RI=(9.76+58.8*Z**(-1.19))*Z
      AI=0.3006*Z*T0/A
      BI=26.96-2.*ALOG(RI)
      CUTM=PCUTM*0.511
C
      CUTM=ALOG(CUTM)
C
      RMI=3.
      X1=3.
      CI=-2.*ALOG(RI/PLE)-1.
      BCI=-CI
      X0=0.2
      IF(BCI.GE.5.215) X0=0.326*BCI-1.5
      SAI=(BCI-4.606*X0)/(X1-X0)**RMI
      RETURN
      END
