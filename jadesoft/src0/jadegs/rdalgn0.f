C   19/02/84 406162322  MEMBER NAME  RDALGN0  (JADEGS)      FORTRAN
C
C-----------------------------------------------------------------------
      SUBROUTINE RDALGN
C-----------------------------------------------------------------------
C
C  AUTHOR:   J. OLSSON    1/11/81 : REMAKE ALGN BANK FOR MC AFTER CUTS
C
C       MOD: J. OLSSON    4/02/84 : IMPROVED VERSION
C       MOD: C. BOWDERY  13/02/84 : PREVENT GRAPHICS MESSAGE PRINTING
C  LAST MOD: C. BOWDERY  19/02/84 : S/R MOVED TO SEPARATE MEMBER
C
C       REMAKE THE BANK ALGN FOR MC EVENTS, KILLING ALL  BLOCKS  BELOW
C       THE READOUT THRESHOLD. THIS IS TO IMITATE REAL DATA READ  OUT.
C       BLOCKS WITH >= 5 COUNTS ARE READ OUT, THE REST ARE  KILLED  TO
C       ALLOW FOR PEDESTAL VARIATION. 5 COUNTS IS ABOUT  25 MEV.  THIS
C       WAS NOT FORESEEN IN MC TRACKING.
C
C       NOTE  THAT  MC  TRACKING  DELIVERS   THE   BANK   ALGN,   WITH
C       PULSEHEIGHTS IN MEV. THEY ARE HERE  CONVERTED  TO  ADC  COUNTS
C       BEFORE THE READ OUT  THRESHOLD  CHECK.  SURVIVING  BLOCKS  ARE
C       TRANSFORMED BACK TO MEV WITH HELP OF THE  AVERAGE  CALIBRATION
C       CONSTANT (C:A 5 MEV/COUNT). THIS GIVES AN EXTRA  "GRANULARITY"
C       EFFECT, WHICH IS OF  SOME  IMPORTANCE  AT  THE  LOWEST  PHOTON
C       ENERGIES.
C
C-----------------------------------------------------------------------
C
      IMPLICIT INTEGER*2 (H)
C
#include "cdata.for"
#include "cgraph.for"
C
      COMMON / CRDSTA / NDUM(12), IPHALG
      COMMON / CTRIGG / IHIST(3)
      COMMON / CHEADR / HEAD(108)
      COMMON / CWORK  / HELP(2,2000)
C
      DIMENSION IHELP(2000)
      EQUIVALENCE (HELP(1,1),IHELP(1))
C
      DATA JELL1/0/,JELL2/0/,JELL3/0/,JELL4/0/,JELL5/0/
      DATA CALC80/5.32/, CALC81/4.94/,ICAL80/5320/, ICAL81/4940/
      DATA IUN80/5/, IUN81/6/
      DATA ICALL/0/
C
C------------------  C O D E  ------------------------------------------
C
      ICALL = ICALL + 1
      IF(HEAD(18).GT.100) GO TO 1000
      IF(IPHALG.EQ.0) GO TO 1000
C
      IPALGN = IDATA(IBLN('ALGN'))
      IF(IPALGN.GT.0) GO TO 1
      JELL1 = JELL1 + 1
      IF(JELL1.LT.5 .AND. NDDINN .EQ. 0 ) WRITE(6,9901)
9901  FORMAT(' * * *  RDALGN MET EVENT  WITHOUT ALGN BANK ')
      GO TO 1000
1     NWO = IDATA(IPALGN)
      IF(NWO.GT.3) GO TO 2
      JELL2 = JELL2 + 1
      IF(JELL2.LT.5) WRITE(6,9902)
9902  FORMAT(' * * *  RDALGN MET EVENT  WITH EMPTY ALGN BANK ')
      GO TO 1000
2     CONTINUE
C
C    LOOP OVER ALGN BANK
C
      IYEAR = IHIST(3)
      IYEAR = MOD(IYEAR,100)
      CALC = CALC80
      ICALC = ICAL80
      IHMC = IUN80
      IF( IYEAR .GT. 80 ) CALC  = CALC81
      IF( IYEAR .GT. 80 ) ICALC = ICAL81
      IF( IYEAR .GT. 80 ) IHMC  = IUN81
      IF( ICALL .GT. 1 .OR. NDDINN .NE. 0 ) GO TO 5710
      WRITE(6,5410) IHIST(3),CALC,IHMC
5410  FORMAT(' ** RDALGN **  YEAR: 'I5,'    CAL. CONST: ',F5.2,'  ',I2,
     +       ' COUNTS THRESHOLD')
5710  IPH = IPALGN*2 + 7
      IPHL = 2*(NWO + IPALGN)
      N1 = 0
      N2 = 0
      N3 = 0
      NWRD = 3
      DO 100 IH = IPH,IPHL,2
      IPU = HDATA(IH+1)
      NBL = HDATA(IH)
      PU = FLOAT(IPU)/CALC
      IPU = IFIX(PU)
      IF(IPU.LT.IHMC) GO TO 100
      IPU = IPU * ICALC + 512
      IPU=SHFTR(IPU,10)
      IF(NBL.GT.0.AND.NBL.LT.2881) GO TO 5
      JELL3 = JELL3 + 1
      IF(JELL3.LT.5) WRITE(6,9903) NBL
9903  FORMAT(' * * *  RDALGN MET EVENT  WITH ILLEGAL BLOCK NR ',I6)
      GO TO 100
5     IF(NBL.LT.2689) N1 = N1 + 1
      IF(NBL.GT.2688.AND.NBL.LT.2785) N2 = N2 + 1
      IF(NBL.GT.2784) N3 = N3 + 1
      NWRD = NWRD + 1
      HELP(1,NWRD) = NBL
      HELP(2,NWRD) = IPU
100   CONTINUE
C
C   SET POINTERS
C
      IHELP(1) = IDATA(IPALGN+1)
      HELP(1,2) = 1
      HELP(2,2) = 1
      IF(N1.GT.0) HELP(2,2) = 2*N1 + 1
      HELP(1,3) = HELP(2,2) + 2*N2
      HELP(2,3) = HELP(1,3) + 2*N3
C
C  RECREATE ALGN
C
      CALL BDLS('ALGN',1)
      CALL BDLS('LGCL',1)
      CALL BGAR(IGA)
      CALL BCRE(KPALGN,'ALGN',1,NWRD,&91,IER)
      CALL BSAW( 1, 'ALGN')
      IF(IER.EQ.0) GO TO 20
C
C---- BCRE ENDS WITH AN ERROR.
C     THE 'ALGN' EXISTS ALREADY.
C
      JELL4 = JELL4 + 1
      IF(JELL4.LT.5) WRITE(6,9904)
9904  FORMAT(' * * *  RDALGN GOT ERROR IN CREATING ALGN BANK ')
      GO TO 1000
C
C---- NOT ENOUGH SPACE IN /BCS/.
   91 JELL5 = JELL5 + 1
      IF(JELL5.LT.5) WRITE(6,9905)
9905  FORMAT(' * * *  RDALGN FOUND NOT ENOUGH SPACE IN BCS ')
      GO TO 1000
C
C---- OVERWRITE 'ALGN'-BANK WITH THE HELP ARRAY
C
  20  CALL MVCL(IDATA,4*KPALGN,IHELP,0,4*NWRD)
C               TO             FROM      BYTES
C
1000  CONTINUE
      RETURN
      END
