C   23/06/78 701051714  MEMBER NAME  CERNKV55 (S)           FORTRAN
      SUBROUTINE CERNKV(DEPTH,DIR,EE,T)
C     S.YAMADA    12-11-77    XX:XX
C
C     CORRECTION OF ANL TO CORRECT VALUE, CORRESPONDING TO N=1.6725
C                                       J.OLSSON   5.1.87
C
C
C---- TRACE CERENKOV LIGHT TO THE BACK SURFACE OF THE LEAD GLASS
C     COUNTER AND EXAMINE PHOTOELECTRIC CONVERSION
C---- DEPTH = INITIAL POINT DEPTH OF THE SOURCE  ELECTRON IN RAD LNG.
C---- DIR = DIRECTION COSIGNS OF THE  SOURCE ELECTRON.
C---- EE = SOURCE ELECTON ENERGY.(USED  FOR HISTOGRAM BINNING ONLY)
C---- T  =  PATH LENGTH OF  THE SOURCE ELECTRON IN RAD LENGTH.
C
      DIMENSION  DIR(3)
C
      COMMON /CONSTB/ CONVMX, BK7, TOTL
C---- CONVMX=MAX CONVERSION EFFICIENCY OF R594.
C---- BK7=LENGTH OF THE BK7 LIGHT GUIDE
C
      COMMON /CCERSR/ SOURCE(14,25),CRNKSC(14,25), ISPCT(14,40),
     1                 NEP,NACP
C---- SOURCE(IDEPTH,IEE)   INPUT ELECTRON SPECTRUM FOR EACH DEPTH.
C---- CRNKSC(IDEPTH,IEE)   CERENKOV CONTRIBUTING ELECTRON SPECTRUM.
C---- ISPCT(IDEPTH,LAMDA)    CERENKOV  LIGHT SPECTRUM SEEN AT
C                          THE BACK SURFACE.IDEPTH=EMMISION DEPTH
C  SF5 PARAMETERS, N=1.6725
      DATA RAD/2.23/,  COST/0.5979/,SINT/0.8016/
C---- DEFAULT VALUE FOR C,S
      DATA C/1.0/,  S/0.0/, ICALL6/0/
C
C---- CALCULATE TOTAL CERENKOV QUANTA NUMBER.
C     NL=(2*3.14/137.)/C*(1-1/N**2)*(DELTA  NEU)*T(IN CM)
C     DELTA NEU =  FREQUENCY RANGE,HERE BETWEEN  429000 AND 1000000
C                  GHZ,DNEU=571000GHZ.
C     N =  REFLACTIVE INDEX (=1.6725 IS USED)
C     (1-1/N**2)=0.6425
C     C=LIGHT VELOCITY(=30CM/NSEC)
C
      ICALL6 = ICALL6 + 1
      TCM =  RAD*T
C     ANL = 592.23*TCM     THIS VALUE CORRESPONDS TO N=1.76 !
      ANL = 560.86*TCM
      IF(ICALL6.EQ.1) WRITE(6,772) ANL
772   FORMAT(' CERNKV55, CORRECTED VERSION FROM 5.1.1987, ANL=',E14.5)
      NL = ANL
      ANL1 = NL
      NL = NL+1
      FRCANL = ANL-ANL1
C
      TCMZ = TCM*ABS(DIR(3))
      DEPTCM = DEPTH*RAD
C----   SET INDECES  FOR HISTOGRAMS
      IDEP  = DEPTH+1.
      IF(IDEP.GT.14) IDEP =  14
      IEE =  5.*ALOG10(EE)+3.
      IF(IEE.LT.1) IEE =  1
      IF(IEE.GT.25) IEE =25
C
      SOURCE(IDEP,IEE) = SOURCE(IDEP,IEE)+T
C
C---- REPEAT FOR NL-PHOTONS
        DO 1 I=1,NL
C----   RANDUMLY CORRECT FRACTIONAL TRIAL NUMBERS.
        IF(I.EQ.NL.AND.RN(DUM).GT.FRCANL) GO TO 1
C
        NEP = NEP+1
C       CALCULATE  PHOTON DIRECTION COSIGN Z-COMPONENT.
        CALL RANAZI(C,S)
        ANN = DIR(3)*COST+(DIR(1)*C+DIR(2)*S)*SINT
C----    CHECK TOTAL REFLECTION CONDITION BETWEEN NORMAL GLASS(N=1.52)
        ABSANN = ABS(ANN)
C-----  TOTAL REFLECTION CHECK IS CHANGED. 02-12-77
        DATA MESS/0/
        IF(MESS.EQ.0) WRITE(6,600)
  600   FORMAT('0******** TRANSPARENCY CHECK AT THE SF5/BK7 BOUNDARY',
     1  /' ',20X,'CHANGED 02-12-77   ********')
        MESS = 1
        IF(ABSANN.LT.0.4172) GO TO 1
        TRNS = 1.-0.0065/(ABSANN-0.410)
        IF(RN(DUM).GT.TRNS) GO TO 1
C
C        SET  FREQUENCY
        FREQ = 429000.+RN(DUM)*571000.
C----    ALAMDA=WAVE LENGTH IN NANO-METER.
        ALAMDA = 3.E8/FREQ
C
C----    EMMITTED POSITION Z-CORDINATE IN CM.
        ZS = DEPTCM+RN(DUM)*TCMZ
        IF(ANN.LT.0.) ZS = -ZS
C        ABSORPTION LENGTH TO THE BACK SURFACE AND THROUGH LIGHT GUIDE
        ABSL = ((TOTL-ZS)/AVABS(ALAMDA)+BK7/BK7ABS(ALAMDA))/ABSANN
        IF(ABSL.LT.0.) GO TO 1
        IF(ABSL.GT.14.) GO TO  1
C
C        ABSORPTION  PROBABILITY  CHECK
        IF(RN(DUM).GT.EXP(-ABSL)) GO TO 1
C         PHOTON REACHES THE BACK SURFACE
C       CHECK CONVERSION
C----   TO KEEP MONTE CARLO EFFICIENCY CONVMX IS MULTIPLIED TO THE RANDU
         IF(CONVMX*RN(DUM).GT.CONVEF(ALAMDA)) GO TO 1
C       PHOTON  IS DETECTED.
         LAMD10 = ALAMDA*0.1-30.
         IF(LAMD10.LT.1) LAMD10 = 1
         IF(LAMD10.GT.40) LAMD10 = 40
         CRNKSC(IDEP,IEE) = CRNKSC(IDEP,IEE)+1.
         ISPCT(IDEP,LAMD10) = ISPCT(IDEP,LAMD10)+1
         NACP = NACP+1
    1    CONTINUE
      RETURN
      END
