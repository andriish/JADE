C   23/06/78 704042204  MEMBER NAME  FNPECER9 (S)           FORTRAN
      SUBROUTINE NPECER(JBE,Z,COST,PENUM,A)
C
C     S.YAMADA    12-12-77
C     LAST MODIFICATION  13-11-78  10:50
C     ----- NEW PARAMETERS CALCULATED BY A.SATO ARE USED NOW -----
C     ----- SEE THE TELEX 13.NOV.78 BY A.SATO. -----
C
C---- CALCULATE NUMBER OF PHOTOELECTRONS BY CERECKOV LIGHT.
C
C INPUT
C     JBE=1 FOR BARREL PART,  =2 FOR END CAP
C     Z=DEPTH OF THE SOURCE TRACK IN THE LEAD GLASS( RAD.LENGTH )
C     COST=COS(THETA) OF THE SOURCE TRACK W.R.TO THE NORMAL OF
C          THE CATHODE SURFACE.
C
C  THIS IS A SPECIAL VERSION WITH AN EXTRA ARGUMENT, FOR PLOTTING A1-2
C
C  UPDATED 4.4.87
C
C   DATA FROM CERSF56.NR1     DONE WITH N=1.6725
C    (NEW BARREL DATA ONLY, B-ARRAY LATER PART FOR ENDCAP STAYS SAME)
C OUTPUT
C     PENUM=NO.OF PHOTOELECTRONS/1RAD.LENGTH LONG TRACK.
C
C---- AN EMPIRICAL FORMULA IS USED.
C---- PHOTOMULTIPLIER AREA IS ASSUMED TO COVER THE WHOLE SURFACE.
C---- TO GET THE REAL PHOTOELECTRON NUMBER PENUM SHOULD BE MULTIPLIED
C     BY THE RATIO (CATHODE AREA/LEAD GLASS CROSS SECTION).
C
      DIMENSION A(2),B(8,2,2)
      DATA B/54.90,48.55,-1180.1, 4484.0, -5803.2,657.17,3772.8,-1950.8,
     1 0.3566,0.624,-1.1146, 9.2361, 2.1321, -22.445,-1.0606, 14.955,
     2   57.43, 18.43, -967.99, 3892.1,-5084.3, 367.12, 3683.6,-1880.3,
     3  0.7039,2.9466,-5.5495,-6.5502, 19.755, 19.114,-17.413,-16.228/
C
      PENUM = 0.
C---- CHECK POSITION
      IF(Z.LT.0. .OR. Z.GT.13.4 ) GO TO 90
      IF(ABS(COST).GT.1.0) GO TO 90
C---- DETERMINE A
      JBEW = JBE
C---- PROTECTION AGAINST FAULS JBE INPUT.
      IF(JBEW.LT.1) JBEW = 1
      IF(JBEW.GT.2) JBEW = 2
        DO 1 IA=1,2
        X = COST
        IF(IA.EQ.1) X = X*X
        F = B(8,IA,JBEW)
          DO 2 J=2,8
          JR = 9-J
          F = F*X+B(JR,IA,JBEW)
    2     CONTINUE
        A(IA) = F
    1   CONTINUE
C---- FUNCTN=A(1)+CC(COST)*EXP(0.55*Z)+A(2)*Z
C
      CC = ((0.8751*COST-0.8362)*COST+0.3924)*COST-2.0
      CC = 10.**CC
      IF(JBEW.EQ.2) CC = 1.5*CC
      EXPARG = EXP(0.55*Z)
      PENUM = A(1)+CC*EXPARG+A(2)*Z
      RETURN
C
      DATA MESS/0/
   90 IF(MESS.EQ.0) WRITE(6,600) JBE,Z,COST
  600 FORMAT(' ***** WRONG INPUT FOR NPECER *****',I6,2E12.3)
      MESS = 1
      RETURN
      END
