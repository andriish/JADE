C   22/11/82 212080650  MEMBER NAME  TWOELS   (JADEGS)      FORTRAN
      SUBROUTINE TWOELS(ICOL,IPLGCL,ERGL)
      IMPLICIT INTEGER*2 (H)
C---
C---        *****  BHABHA REJECTION IN REDUC 2  ********
C---
C---  CHECK WHETHER COLLINEAR CLUSTERS WITH HIGH ENERGY EXIST
C---  IPLGCL = POINTER TO LGCL
C---  ICOL = 0 IF NO COLLINEARS
C---  ICOL = 1 IF COLLINEARS IN BARREL
C---  ICOL = 2 IF COLLINEARS IN ENDCAP
C---                                              J.OLSSON  22.11.82
#include "cdata.for"
      COMMON /CJTRIG/ PI,TWOPI
      DATA FIDEL /.200/
      DATA ZLM /500./
      DATA XLM /350./
      DATA YLM /350./
C---
      ICOL = 0
      NCLST = IDATA(IPLGCL+7)
      IF(NCLST.LE.1) RETURN
      IP3 = IDATA(IPLGCL+3)
      NPWCL = IDATA(IPLGCL+25)
      MARK = 0
      NNCL = NCLST - 1
C
      DO 610  I = 1,NNCL
      IB1 = IPLGCL + IP3 + (I-1)*NPWCL - 1
      IF(ADATA(IB1+2).LT.ERGL) GO TO 610
      JPRT1 = IDATA(IB1+1)
      NN = I + 1
      DO 611  J = NN,NCLST
      IB2 = IPLGCL + IP3 + (J-1)*NPWCL - 1
      JPRT2 = IDATA(IB2+1)
      IF(JPRT1+JPRT2.NE.0) GO TO 611
      IF(ADATA(IB2+2).LT.ERGL) GO TO 611
      IF(JPRT1.NE.0) GO TO 612
C
C  CHECK OF TWO CLUSTERS IN BARREL
C
      FI1 = ADATA(IB1+4)
      FI2 = ADATA(IB2+4)
      ZE1 = ADATA(IB1+5)
      ZE2 = ADATA(IB2+5)
      ABF = ABS(FI2-FI1)
      ABZ = ABS(ZE1+ZE2)
      IF(ABZ.GT.ZLM) GO TO 611
      IF(ABF.LT.PI-FIDEL.OR.ABF.GT.PI+FIDEL) GO TO 611
      ICOL = 1
      GO TO 619
C
C  CHECK OF TWO CLUSTERS IN ENDCAPS
C
612   X1 = ADATA(IB1+4)
      X2 = ADATA(IB2+4)
      Y1 = ADATA(IB1+5)
      Y2 = ADATA(IB2+5)
      ABX = ABS(X1+X2)
      ABY = ABS(Y1+Y2)
      IF(ABX.GT.XLM) GO TO 611
      IF(ABY.GT.YLM) GO TO 611
      ICOL = 2
      GO TO 619
611   CONTINUE
610   CONTINUE
C
619   RETURN
      END
